apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'
 
group = 'etri'
version = '5.12.02'

sourceCompatibility = JavaVersion.VERSION_1_8;
[compileJava,compileTestJava]*.options*.encoding = 'UTF-8'

buildscript {
	repositories {
		mavenLocal()
		jcenter()
	}
	dependencies {
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
	}
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.16.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

repositories {
    mavenLocal()
	maven {
		url "http://www.datanucleus.org/downloads/maven2/"
	}
	maven {
		url "http://download.osgeo.org/webdav/geotools/"
	}
    mavenCentral()
}


ext.grpc_version = '1.16.1'
ext.protobuf_version = '3.5.1'
ext.geotools_version = '17.2'
ext.jts_version = '1.13'
ext.commons_csv_version = '1.5'
ext.httpclient_version = '4.5.6'
ext.joda_time_version = '2.10'
ext.picocli_version = '3.9.6'
ext.univocity_version = '2.8.1'
ext.postgresql_version = '42.0.0'
ext.postgis_jdbc_version = '2.1.0'

dependencies {
	api project(':utils')
	
	api "com.vividsolutions:jts:${jts_version}"

	// GeoTools, JTS, Proj4J
	api	"org.geotools:gt-opengis:${geotools_version}"
	api	"org.geotools:gt-api:${geotools_version}"
	api	"org.geotools:gt-epsg-hsql:${geotools_version}"
	api "org.geotools:gt-shapefile:${geotools_version}"
	api	"org.geotools:gt-geojson:${geotools_version}"

	// Jackson
//	compile "com.fasterxml.jackson.core:jackson-databind:2.6.5"

	// Misc
	implementation "org.apache.commons:commons-csv:${commons_csv_version}"
	api "org.apache.httpcomponents:httpclient:${httpclient_version}"
	
	// grpc
	api "com.google.protobuf:protobuf-java:${protobuf_version}"
	implementation "com.google.protobuf:protobuf-java-util:${protobuf_version}"
	api "io.grpc:grpc-stub:${grpc_version}"
	api "io.grpc:grpc-netty-shaded:${grpc_version}"
	implementation "io.grpc:grpc-protobuf:${grpc_version}"
	
	// PostSQL
//	compile "org.postgresql:postgresql:${postgresql_version}"
//	compile "org.postgis:postgis-jdbc:${postgis_jdbc_version}"
	
	// Joda-Time
	implementation "joda-time:joda-time:${joda_time_version}"
	
	// StringTemplate4
	implementation "org.antlr:ST4:4.0.8"
	
	// command line parser
	api "info.picocli:picocli:${picocli_version}"
	
	testImplementation "junit:junit:4.12"
}

task sourceJar(type: Jar) {
	classifier 'sources'
	from sourceSets.main.allSource
}

javadoc {
	source sourceSets.main.allJava
	
	title = 'Marmot API Document'
	
	options.charSet = 'UTF-8'
	options.encoding = 'UTF-8'
	options.links = ['https://docs.oracle.com/javase/8/docs/api/']
}
task javadocJar(type: Jar) {
	classifier 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}

apply plugin: 'maven-publish'
publishing {
	publications {
		main(MavenPublication) {
			components.java
			
			artifact sourceJar
			artifact javadocJar
		}
	}
}

apply plugin: 'eclipse'
eclipse {
	classpath {
		file {
			withXml {
				def node = it.asNode()
				node.appendNode('classpathentry', [kind: 'output', path: 'classes'])
			}
			
			whenMerged { cp ->
				Map entryByPath = cp.entries.groupBy { entry -> entry.path }
				entryByPath.each { key, values ->
					if ( values.size() > 1 ) {
            			def entry = values.first()
            			if ( entry.kind == 'src' ) {
            				entry.includes = []
            				entry.excludes = []
            			}
            			int index = cp.entries.indexOf entry
            			cp.entries.removeAll values
            			cp.entries.add index, entry
					}
				}
				cp.entries.each { entry ->
					if ( entry.kind == 'src' && entry.hasProperty('output') ) {
						entry.output = 'classes'
					}
				}
			}
		}
	}
}
